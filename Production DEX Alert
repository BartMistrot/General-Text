CREATE PROCEDURE dbo.dex_file_latency_threshold_exceeded AS
BEGIN
    -- Select the most recent record within the last hour based on transitdatetime
    SELECT TOP 1 *
    INTO #temp
    FROM DEX_JMS_FC..dex_monitor
    WHERE transitdatetime > DATEADD(HOUR, -1, GETDATE())
    ORDER BY transitdatetime DESC;

    -- Check if any record was found
    IF EXISTS (SELECT 1 FROM #temp)
    BEGIN
        -- Check if ackDateTime is more than 1 hour old
        IF EXISTS (
            SELECT 1
            FROM #temp
            WHERE ackDateTime < DATEADD(HOUR, -1, GETDATE())
        )
        BEGIN
            -- Prepare and send the email alert
            DECLARE @xml NVARCHAR(MAX);
            DECLARE @body NVARCHAR(MAX);

            SET @xml = CAST((
                SELECT
                    [msgID] AS 'td','',
                    [activityOccured] AS 'td','',
                    [transitDateTime] AS 'td','',
                    [transmitDateTime] AS 'td','',
                    [ackDateTime] AS 'td','',
                    [status] AS 'td',''
                FROM #temp
                FOR XML PATH('tr'), ELEMENTS
            ) AS NVARCHAR(MAX));

            SET @body = '<html><body><H3>JobStream Monitor Error Alert</H3>
            <table border="1">
                <tr>
                    <th>msgID</th>
                    <th>activityOccured</th>
                    <th>transitDateTime</th>
                    <th>transmitDateTime</th>
                    <th>ackDateTime</th>
                    <th>status</th>
                </tr>'
                + @xml +
            '</table></body></html>';

            EXEC msdb.dbo.sp_send_dbmail
                @profile_name = 'GHSMAIL',
                @recipients = 'IS-DBA@brgeneral.onmicrosoft.com',
                @body = @body,
                @body_format ='HTML',
                @subject = 'Please create a P2 and assign to BRG-REV - SOC - DEX File latency Detected';
        END
        ELSE
        BEGIN
            -- ackDateTime is within the last hour; no action needed
            SELECT 0;
        END
    END
    ELSE
    BEGIN
        -- No records found within the last hour; no action needed
        SELECT 0;
    END

    DROP TABLE #temp;
END
